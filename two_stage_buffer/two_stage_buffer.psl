vunit i_two_stage_buffer(two_stage_buffer(synthesis))
{
   -- Additional signals used during formal verification
   signal f_last_value : std_logic_vector(G_DATA_SIZE-1 downto 0) := (others => '0');
   signal f_count      : integer range 0 to 3 := 0;
   signal f_data_next  : std_logic_vector(G_DATA_SIZE-1 downto 0) := (others => '0');


   -- set all declarations to run on clk_i
   default clock is rising_edge(clk_i);


   -----------------------------
   -- ASSERTIONS ABOUT OUTPUTS
   -----------------------------

   -- FIFO must be empty after reset, unless incoming data
   f_after_reset_empty : assert always {rst_i} |=> {not m_valid_o} abort s_valid_i;

   -- FIFO must be ready after reset
   f_after_reset_ready : assert always {rst_i} |=> s_ready_o;

   -- Output must be stable until accepted or reset
   f_output_stable : assert always {m_valid_o and not m_ready_i and not rst_i} |=> {stable(m_valid_o) and stable(m_data_o)};

   -- Keep track of amount of data flowing into and out of the FIFO
   p_count : process (clk_i)
   begin
      if rising_edge(clk_i) then
         -- Data flowing in, but not out.
         if s_valid_i and s_ready_o and not (m_valid_o and m_ready_i) then
            f_count <= f_count + 1;
         end if;

         -- Data flowing out, but not in.
         if m_valid_o and m_ready_i and not (s_valid_i and s_ready_o) then
            f_count <= f_count - 1;
         end if;

         if rst_i then
            f_count <= 0;
         end if;
      end if;
   end process p_count;

   -- Keep track of data flowing into and out of the FIFO
   p_last_value : process (clk_i)
   begin
      if rising_edge(clk_i) then
         -- Remember last value written into FIFO
         if s_valid_i and s_ready_o then
            f_last_value <= s_data_i;
         end if;
      end if;
   end process p_last_value;

   -- The FIFO size is limited to 2.
   f_size : assert always {0 <= f_count and f_count <= 2};

   -- If FIFO is full, ready must be transparent
   f_count_2 : assert always {f_count >= 2} |-> {s_ready_o = m_ready_i} abort rst_i;

   -- If FIFO is almost full, it must always present valid data on output
   f_count_1 : assert always {f_count >= 1} |-> {m_valid_o = '1'} abort rst_i;

   -- If FIFO is empty and no input, no data present on output
   f_count_0 : assert always {f_count = 0 and s_valid_i = '0'} |-> {m_valid_o = '0'} abort rst_i;

   -- If FIFO is empty and with input, valid data present on output
   f_count_0_input : assert always {f_count = 0 and s_valid_i = '1'} |-> {m_valid_o = '1' and m_data_o = s_data_i} abort rst_i;

   f_int2 : assert always {f_count = 2} |-> {    int_afull and     s_afull_o} abort rst_i;
   f_int1 : assert always {f_count = 1} |-> {    int_afull and not s_afull_o} abort rst_i;
   f_int0 : assert always {f_count = 0} |-> {not int_afull and not s_afull_o} abort rst_i;

   f_fill : assert always {f_count = to_integer(s_fill_o)} abort rst_i;


   -----------------------------
   -- ASSUMPTIONS ABOUT INPUTS
   -----------------------------

   -- Require reset at startup.
   f_reset : assume {rst_i};

   p_data : process (clk_i)
   begin
      if rising_edge(clk_i) then
         if s_valid_i and s_ready_o then
            f_data_next <= s_data_i + 1;
         end if;
      end if;
   end process p_data;

   f_data : assume always {s_data_i = f_data_next};

   f_input_stable : assume always {s_valid_i and not s_ready_o} |=> {stable(s_valid_i) and stable(s_data_i)};


   --------------------------------------------
   -- COVER STATEMENTS TO VERIFY REACHABILITY
   --------------------------------------------

   -- Make sure FIFO can transition from full to empty.
   f_full_to_empty : cover {m_valid_o and not rst_i; not m_valid_o};
   f_back2back : cover {m_valid_o and m_ready_i; m_valid_o};

} -- vunit i_two_stage_buffer(two_stage_buffer(synthesis))

